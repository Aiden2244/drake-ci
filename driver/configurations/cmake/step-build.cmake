begin_stage(
  PROJECT_NAME "Drake"
  BUILD_NAME "${DASHBOARD_BUILD_NAME}")

ctest_update(SOURCE "${CTEST_SOURCE_DIRECTORY}" QUIET)

file(WRITE "${CTEST_BINARY_DIRECTORY}/CMakeCache.txt" "${CACHE_CONTENT}")

ctest_configure(BUILD "${CTEST_BINARY_DIRECTORY}"
  OPTIONS --warn-uninitialized
  SOURCE "${CTEST_SOURCE_DIRECTORY}"
  RETURN_VALUE DASHBOARD_CONFIGURE_RETURN_VALUE
  CAPTURE_CMAKE_ERROR DASHBOARD_CONFIGURE_CAPTURE_CMAKE_ERROR
  QUIET)

if(NOT DASHBOARD_CONFIGURE_RETURN_VALUE EQUAL 0 OR DASHBOARD_CONFIGURE_CAPTURE_CMAKE_ERROR EQUAL -1)
  append_step_status("CMAKE CONFIGURE" FAILURE)
endif()

ctest_read_custom_files("${CTEST_BINARY_DIRECTORY}")

ctest_build(BUILD "${CTEST_BINARY_DIRECTORY}"
  NUMBER_ERRORS DASHBOARD_BUILD_NUMBER_ERRORS
  NUMBER_WARNINGS DASHBOARD_BUILD_NUMBER_WARNINGS
  RETURN_VALUE DASHBOARD_BUILD_RETURN_VALUE
  CAPTURE_CMAKE_ERROR DASHBOARD_BUILD_CAPTURE_CMAKE_ERROR
  QUIET)

if(DASHBOARD_BUILD_NUMBER_ERRORS GREATER 0 OR NOT DASHBOARD_BUILD_RETURN_VALUE EQUAL 0 OR DASHBOARD_BUILD_CAPTURE_CMAKE_ERROR EQUAL -1)
  append_step_status("CMAKE BUILD" FAILURE)
endif()

if(MATLAB)
  set(CTEST_CUSTOM_MAXIMUM_FAILED_TEST_OUTPUT_SIZE 307200)
  set(CTEST_CUSTOM_MAXIMUM_PASSED_TEST_OUTPUT_SIZE 307200)

  ctest_test(BUILD "${CTEST_BINARY_DIRECTORY}/drake_matlab-build" ${CTEST_TEST_ARGS}
    RETURN_VALUE DASHBOARD_TEST_RETURN_VALUE
    CAPTURE_CMAKE_ERROR DASHBOARD_TEST_CAPTURE_CMAKE_ERROR
    QUIET)
endif()

ctest_submit(RETRY_COUNT 4 RETRY_DELAY 15 QUIET)
